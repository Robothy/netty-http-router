import io.franzbecker.gradle.lombok.task.DelombokTask

import java.nio.file.Files

plugins {
    id 'java'
    id 'maven-publish'
    id "com.robothy.cn-repo" version "1.8"
    id "robothy-platform-release"
    id "signing"
    id 'io.franzbecker.gradle-lombok' version '5.0.0'
}

group 'io.github.robothy'

repositories {
    mavenLocal()
    mavenCentral()
}

ext {
    lombokVersion = "1.18.24"
    nettyVersion = '4.1.77.Final'
    jupiterVersion = "5.8.1"
    slf4jVersion = "1.7.36"
    mockitoVersion = "3.4.0"
    sprintframeworkVersion = "5.3.23"
}

dependencies {
    implementation "io.netty:netty-all:${nettyVersion}"
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    implementation "org.slf4j:slf4j-api:${slf4jVersion}"
    implementation "org.slf4j:slf4j-simple:${slf4jVersion}"
    implementation "org.springframework:spring-web:${sprintframeworkVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${jupiterVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-params:${jupiterVersion}"
    testImplementation "org.mockito:mockito-inline:${mockitoVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${jupiterVersion}"
}

test {
    useJUnitPlatform()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(15)
    }

    withSourcesJar()
    withJavadocJar()
}

publishing {

    publications {
        maven(MavenPublication) {
            groupId = group
            artifactId = project.name
            from components.java
          //  artifact sourcesJar

            pom {
                name = "Netty Http Router"
                description 'A Netty HTTP router library.'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'robothy'
                        name = "Fuxiang Luo"
                        email = "robothyluo@gmail.com"
                    }
                }

                scm {
                    url = "https://github.com/Robothy/netty-http-router"
                }

            }
        }
    }

    repositories {
        mavenLocal()
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/Robothy/netty-http-router")
            credentials {
                username = project.findProperty("GITHUB_USERNAME") ?: System.getenv("GITHUB_USERNAME")
                password = project.findProperty("GITHUB_TOKEN") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

signing {
    def signingKey
    if (findProperty("signingKeyRingFile") != null) {
        signingKey = Files.readString(file(findProperty("signing.secretKeyRingFile")).toPath())
    } else {
        signingKey = findProperty("signing.secretKey")
    }
    println signingKey
    def signingPassword = findProperty("signing.password")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.maven
}


task delombok(type: DelombokTask, dependsOn: compileJava) {
    ext.outputDir = file("$buildDir/delombok")
    outputs.dir(outputDir)
    sourceSets.main.java.srcDirs.each {
        inputs.dir(it)
        args(it, "-d", outputDir)
    }
}

javadoc {
    dependsOn delombok
    source = delombok.outputDir
    //failOnError = false
}